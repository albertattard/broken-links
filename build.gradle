plugins {
    id "java"
    id "application"
    id "idea"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

application {
    mainClass = "quickhacks.Main"
}

jar {
    manifest {
        attributes("Main-class": "quickhacks.Main")
    }
}

sourceSets {
    testEndToEnd {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/testEndToEnd/java')
        }
        resources.srcDir file('src/testEndToEnd/resources')
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.testEndToEnd.java.srcDirs
        testSourceDirs += project.sourceSets.testEndToEnd.resources.srcDirs
    }
}

configurations {
    testEndToEndImplementation.extendsFrom testImplementation
    testEndToEndRuntimeOnly.extendsFrom testRuntimeOnly
}

test {
    useJUnitPlatform()

    testLogging {
        events = ['FAILED', 'PASSED', 'SKIPPED', 'STANDARD_OUT']
    }
}

task testEndToEnd(type: Test) {
    group = 'verification'
    description = 'Runs the End-to-End tests.'
    testClassesDirs = sourceSets.testEndToEnd.output.classesDirs
    classpath = sourceSets.testEndToEnd.runtimeClasspath
    outputs.upToDateWhen { false }
    dependsOn jar
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events = ['FAILED', 'PASSED', 'SKIPPED', 'STANDARD_OUT']
    }
}

check {
    dependsOn testEndToEnd
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.0-M1"
    testImplementation "org.assertj:assertj-core:3.19.0"
    testImplementation "org.mock-server:mockserver-junit-jupiter:5.11.2"
}
